! *****************************************************************************
module ForwardSolver
  !  High level interface/control module used by top level routines
  !   for initializing and using the solver.  The key public routines
  !   in this module have only generic (abstract data type) arguments
  !   and can thus be called from the top-level inversion routines.
  !
  !  All public routines must have the same generic names, parameter lists
  !   and abstract functionality; this implementation is for EARTH
  !
  !  Idea is to always call initSolver before calling fwdSolver,
  !   in particular before the first solution for each transmitter
  !   (frequency).  If called for the first time (in a program run,
  !   or after a call to exitSolver), full initialization
  !   (after deallocation/cleanup if required) is performed.

  use math_constants
  use SolnSpace
  use jacobian
  use transmitters
  use input
  use output
  use UserData
  use initFields
  use modelmap

  implicit none

  public :: initSolver, fwdSolve, sensSolve, exitSolver

  save
  private
  type(timer_t)     :: timer
  logical           :: solverInitialized = .false.
  logical           :: secondaryField = .false.
  logical           :: newModelParam = .false.
  type(cvector)     :: b, e
  type(rvector)     :: drhoF
  type(rscalar)     :: rho1d, drho
  type(modelParam_t):: m1d
  type(rscalar)     :: rho ! resistivity on the grid
  type(cvector)     :: source ! user-specified interior source
  type(sparsevecc)  :: BC ! boundary conditions set from P10
  type(modelParam_t)    :: mPrior  ! use it to get the radial 1d model for SFF
  type(modelParam_t)    :: mPrev  ! store the previous model for efficiency
  type(solnVector_t)    :: hPrev  ! store the previous forward solver solution
  !type(rhsVector_t)     :: b   ! needed to store the RHS for forward solver
  type(solnVector_t) :: h1d, dh ! needed for secondary field formulation

Contains

  !**********************************************************************
  subroutine initSolver(iTx,m0,grid,h0,h,comb)
   !  Initializes both forward and sens solvers for transmitter iTx.

   integer, intent(in)                      :: iTx
   type(modelParam_t),intent(in), target	:: m0
   type(grid_t), intent(in), target         :: grid
   type(solnVector_t), intent(inout), optional :: h0
   type(solnVector_t), intent(inout), optional :: h
   type(rhsVector_t), intent(inout), optional :: comb
   !  local variables
   type(transmitter_t), pointer             :: freq
   logical                                  :: initFwd, initForSens

   initFwd = present(h0)
   initForSens = present(comb)

   ! First of all, initialize a local copy of the GRID.
   ! All vectors will point to that grid, which should be
   ! local to each processor.
   !grid = igrid

   freq => freqList%info(iTx)
   write(*,'(a12,a46,es9.3,a5)') node_info, &
        'Initializing 3D SGFD global solver for period ',freq%period,' days'

   ! If h0 is already allocated, do not reinitialize - use the previous
   ! forward solution as starting solution for this frequency ...
   if(initFwd) then
     if(.not. h0%allocated) then
        call create_solnVector(grid,iTx,h0)
        call initialize_fields(grid,h0%vec,BC)
     endif
   endif

   if(initForSens) then
        ! also initialize the optional outputs
      call create_solnVector(grid,iTx,h)
      comb%nonzero_source = .true.
      call create_rhsVector(grid,iTx,comb)
   endif

   secondaryField = freq%secondaryField

   if(.not. solverInitialized) then
      ! only do this when called for the first time
      mPrior = m0
      if(secondaryField) then
	    ! Make 1D parametrization out of full, and map to grid (primary cell centers)
	    call read_modelParam(mPrior,cUserDef%fn_param0)
	    m1d = getRadial(mPrior)
	    call create_rscalar(grid,rho1d,CENTER)
	    call initModel(grid,m1d,rho1d)
	  endif
      solverInitialized = .true.
   endif

   if(secondaryField) then
      call read_solnVector(cUserDef%fn_field,grid,iTx,h1d)
   endif

   ! this will only be true when new model is supplied (m0 /= mPrev)
   mPrev = m0
   newModelParam = .true.

   if(newModelParam) then
      ! compute the resistivity on the grid
      call create_rscalar(grid,rho,CENTER)
      call initModel(grid,m0,rho)

      if(secondaryField) then
	    ! Take the difference on the grid, to avoid the problem with zero resistivity
	    call create_rscalar(grid,drho,CENTER)
	    call linComb_rscalar(ONE,rho,MinusONE,rho1d,drho)
	    !call outputModel('test.rho',grid,drho%v)

	    ! Map the resistivity vector to primary cell faces (dual edges)
	    call operatorL(drho,drhoF,grid)
	  endif

   endif

   ! Initialize interior source from file (currently, set to zero)
   if(.not. source%allocated) then
    call create_cvector(grid,source,EDGE)
   endif

   ! reset timer
   call reset_time(timer)

  end subroutine initSolver

  !**********************************************************************
  subroutine fwdSolve(iTx,h)
!  Forward solver; uses b as the RHS. Similar to sensSolve, less general.
!  Temporary routine (will be merged with sensSolve).

   integer, intent(in)                        :: iTx
   type(solnVector_t), intent(inout)            :: h
   ! local variables
   type(transmitter_t), pointer                 :: freq
   real(kind=prec)                              :: omega
   logical                                      :: adjoint,sens

   ! IMPORTANT: FIRST update pointer to the transmitter in solnVector
   h%tx = iTx

   freq => freqList%info(iTx)

   ! run FWD/ADJ solver
   write (*,'(a12,a12,a3,a20,i4,a2,es12.6,a5)') node_info, &
    'Solving the ',FWD,' problem for period ',iTx,': ',1/freq%value,' secs'

   omega  = 2.0d0*pi*freq%value     ! angular frequency (radians/sec)

   if (secondaryField) then

      write(*,*) node_info, 'Using the secondary field formulation ...'

      ! Compute the RHS = - del x drho (del x H)
      h = h1d
      call operatorD_l_mult(h%vec,h%grid)
      call operatorC(h%vec,e,h%grid)
      call diagMult(drhoF,e,e)
      call operatorCt(e,b,h%grid)
      call operatorD_Si_divide(b,h%grid)
      call linComb(C_MinusONE,b,C_ZERO,b,b)

      ! solve S_m <h> = <b> for vector <h>
      dh = h
      call zero_solnVector(dh)
      adjoint = .false.
      sens = .true.
      call linComb_cvector(C_ONE,source,C_ONE,b,b)
      call operatorMii(dh%vec,b,omega,rho,h%grid,fwdCtrls,h%errflag,adjoint)

      ! Full solution for one frequency is the sum H1D + dH
      call linComb_solnVector(C_ONE,h1d,C_ONE,dh,h)

   else

      write(*,*) node_info, 'Using the forward solver ...'

      ! solve S_m <h> = <s> for vector <h>
      adjoint = .false.
      sens = .true.
      call operatorMii(h%vec,source,omega,rho,h%grid,fwdCtrls,h%errflag,adjoint,BC)

   end if

   ! compute and output fields & C and D responses at cells
   call outputSolution(freq,h%vec,slices,h%grid,cUserDef,rho%v,'h')

   ! output full H-field cvector
   if (output_level > 3) then
      call write_solnVector(cUserDef%modelname,h)
   end if

   if (output_level > 1) then
      write (*,*) node_info, ' time taken (mins) ', elapsed_time(timer)/60.0
   end if
  end subroutine fwdSolve

  !**********************************************************************
  subroutine sensSolve(iTx,FWDorADJ,h,comb)
!  Generic forward solver; if comb is specified, it is used as forcing;
!  otherwise, assume that the RHS is already stored in b.
!  Use the input e for starting solution
!  (normally, e will be zero on input for sensitivities).

   integer, intent(in)                        :: iTx
   character (len=3), intent(in)                :: FWDorADJ
   type(solnVector_t), intent(inout)            :: h
   type(rhsVector_t), intent(inout), optional    :: comb
   ! local variables
   type(transmitter_t), pointer                 :: freq
   real(kind=prec)                              :: omega
   logical                                      :: adjoint,sens

   ! update pointer to the transmitter in solnVector
   h%tx = iTx

   freq => freqList%info(iTx)

   ! run FWD/ADJ solver
   write (*,'(a12,a12,a3,a20,i4,a2,es12.6,a5)') node_info, &
    'Solving the ',FWDorADJ,' problem for period ',iTx,': ',1/freq%value,' secs'

   omega  = 2.0d0*pi*freq%value     ! angular frequency (radians/sec)

   ! solve S_m <h> = <s> for vector <h>
   if(.not. present(comb)) then

    ! assume interior forcing s + BC; starting solution already initialized
    adjoint = .false.
    sens = .false.
    call operatorMii(h%vec,source,omega,rho,h%grid,fwdCtrls,h%errflag,adjoint,BC)

   else

    ! use comb for forcing and assume zero BC; starting solution should be zero
    adjoint = (FWDorADJ .ne. FWD)
    sens = .true.
    call operatorMii(h%vec,comb%source,omega,rho,h%grid,fwdCtrls,h%errflag,adjoint)

   endif

   if (output_level > 1) then
      write (*,*) node_info, ' time taken (mins) ', elapsed_time(timer)/60.0
   end if


  end subroutine sensSolve


  !**********************************************************************
  subroutine exitSolver(h0,h,comb)
   ! Cleans up after fwdSolver and deallocates all solver data. Call
   ! initSolver to update solver data; call this to exit completely.
   ! Optionally, deallocates e0,e,comb
   type(solnVector_t), intent(inout), optional  :: h0
   type(solnVector_t), intent(inout), optional  :: h
   type(rhsVector_t), intent(inout), optional   :: comb

   ! local variables
   logical          :: initForSens

   initForSens = present(comb)

   if(present(h0)) then
      call deall_solnVector(h0)
   endif

   if(initForSens) then
      call deall_rhsVector(comb)
      call deall_solnVector(h)
   endif

   if(solverInitialized) then
      ! cleanup/deallocation routines for model operators
      if(secondaryField) then
        call deall_solnVector(h1d)
        call deall_solnVector(dh)
        call deall_cvector(b)
        call deall_cvector(e)
        call deall_rvector(drhoF)
        call deall_rscalar(rho1d)
        call deall_rscalar(drho)
        call deall_modelParam(m1d)
      endif
      !call deall_rhsVector(b)
      call deall_solnVector(hPrev)
      call deall_modelParam(mPrev)
      call deall_modelParam(mPrior)
      call deall_cvector(source)
      call deall_sparsevecc(BC)
      call deall_rscalar(rho)
      solverInitialized = .false.
      newModelParam = .false.
   endif

   ! restart the clock
   call clear_time(timer)

  end subroutine exitSolver


end module ForwardSolver
