! *****************************************************************************
module jacobian
  ! Module containing the operators required for the Jacobian and penalty
  ! functional derivative calculations

  use modeldef
  use model_operators
  use elements
  use dimensions
  use griddef
  use sg_vector
  use sg_scalar
  use sg_spherical
  use wrapper
  implicit none


Contains

  ! ***************************************************************************
  ! * This is the operator M (for Maxwell's equations). It is more general than
  ! * operator $A_{\rho,\omega}$ : E_i -> E_i (implemented as the subroutine
  ! * SolveMaxwells()). Operator $A_{\rho,\omega}$ acts on a vector defined on
  ! * internal edges, to produce an output also defined on internal edges.
  ! * It would not need to know anything about the input and output vectors,
  ! * if it didn't do the divergence correction, for which the pure values of
  ! * the internal & external magnetic fields and the forcing are required.
  ! * If the adjoint of this operator is required, it is obtained by setting
  ! * the omega which is passed on to the operator to -omega. Therefore, we pass
  ! * the information to operator $A_{\rho,\omega}$ whether it is an adjoint or
  ! * the forward solution that we would like to obtain. If it is in fact an
  ! * adjoint, two things happen inside SolveMaxwells(). First, omega is set to
  ! * minus omega; second, divergence correction takes into account that to obtain
  ! * the pure fields different transformations are required.
  ! *
  ! * Operator M here is the operator the solves the system of equations.
  ! * The input to this operator is assumed to be the boundary conditions (pure
  ! * fields on boundaries), and pure forcing. The output is the internal and
  ! * boundary magnetic fields (both are saved in the same vector H). For the
  ! * forward operator the boundary magnetic fields will always be the same as
  ! * the input boundary conditions. However, for the adjoint this will not always
  ! * be the case. In fact, the boundary magnetic fields will be computed using
  ! * both the input boundary conditions and the computed internal magnetic field.
  ! * This has not yet been implemented. Currently, for the adjoint, the boundary
  ! * magnetic fields will always be assumed zero; since for the adjoint the
  ! * solution on the internal nodes does not depend on them, except through the
  ! * divergence correction.

  subroutine operatorM(Xfull,Yfull,omega,rho,grid,fwdCtrls,errflag,adjflag,sens)

	use maxwells, only: SolveMaxwells
	use field_vectors
	use boundaries
	implicit none

	type (cvector), intent(inout)			  :: Xfull
	type (cvector), intent(in)				  :: Yfull
	type (sparsevecc)						  :: Xb,Yb
	type (grid_t), intent(in)			  :: grid
	real(8), intent(in)						  :: omega
	real(8)									  :: om
	real(8), dimension(:,:,:), intent(in)	  :: rho
	type (fwdCtrl_t), intent(in)			  :: fwdCtrls
	integer, intent(inout)					  :: errflag
	logical, intent(inout), optional		  :: adjflag
	logical, intent(inout), optional		  :: sens
	logical									  :: adjoint,delta
	complex(8),dimension(:),allocatable		  :: vectorx,vectory,vectorb,vectors,vectorh
	integer									  :: istat,nx,ny,nz

	! Indicator of whether the forward solver or the adjoint has been called
	if(.not.present(adjflag)) then
	  adjoint = .FALSE.
	else
	  adjoint = adjflag
	end if
	! Indicator of whether we are computing the data sensitivities;
	! if so, starting solution and boundary conditions are zero.
	if(.not.present(sens)) then
	  delta = .FALSE.
	else
	  delta = sens
	end if

	! Check that input and output are initialized
    if(.not.Yfull%allocated) then
       write(0,*) 'Error: (operatorM) Input vector not allocated, exiting...'
       stop
    endif
    if(.not.Xfull%allocated) then
       ! Output vector not allocated, initializing now...
	   call create_cvector(grid,Xfull,EDGE)
    endif

	allocate(vectorx(np2),vectory(np2),STAT=istat)
	allocate(vectorb(np2),vectors(np2),vectorh(np2),STAT=istat)
	vectorx = C_ZERO
	vectory = C_ZERO
	vectorb = C_ZERO
	vectors = C_ZERO
	vectorh = C_ZERO

	nx = grid%nx
	ny = grid%ny
	nz = grid%nz

	!-----------------------------------
	! Set up the RHS <y> for A <x> = <y>
	!-----------------------------------
	call copyd3_d1_d(vectors,Yfull,grid)  ! extract interior components
	call extractBC(Yb,Yfull)			  ! extract boundary components
	call calcb_from_bc(nx,ny,nz,Yb,vectorb,rho,grid%x,grid%y,grid%z)
	vectory = vectors
	if (adjoint) then
	  call divide_vec_by_l(nx,ny,nz,vectory,grid%x,grid%y,grid%z)
	else
	  call mult_vec_by_S(nx,ny,nz,vectory,grid%x,grid%y,grid%z)
	  vectory = vectory + vectorb
	end if

	!------------------------------------------------
	! Set up the initial value of <x> for A <x> = <y>
	!------------------------------------------------
	call copyd3_d1_d(vectorh,Xfull,grid)  ! extract interior components
	call extractBC(Xb,Xfull)			  ! extract boundary components
	vectorx = vectorh
	if (adjoint) then
	  call divide_vec_by_S(nx,ny,nz,vectorx,grid%x,grid%y,grid%z)
	else
	  call mult_vec_by_l(nx,ny,nz,vectorx,grid%x,grid%y,grid%z)
	end if

	!-------------------------------------------------------------------
	! Perform all preliminary computations for the divergence correction
	!-------------------------------------------------------------------
	! Regardless of whether adjoint or not, recompute vectors for div(s)
	vectors = vectory - vectorb
	call divide_vec_by_S(nx,ny,nz,vectors,grid%x,grid%y,grid%z)
	call copyd1_d3_b(nx,ny,nz,sx,sy,sz,vectors,grid%x,grid%y,grid%z)
	! Set initial conditions for the divergence correction (stored in Xfull)
	call copyd1_d3_b(nx,ny,nz,hx,hy,hz,vectorh,grid%x,grid%y,grid%z)
	! This step is only needed while the divergence correction uses b.c.
	if (.not.delta) then
	  Xb = Yb ! then use original boundary conditions for H
	end if
	call insertBoundaryValues(Xb,hx,hy,hz)


	!----------------------
	! Set the sign of omega
	!----------------------
	if (adjoint) then
	  om = - omega
	else
	  om = omega
	end if

	!------------------------------------------------
	! Call the forward solver $A_{\rho,\omega} x = y$
	!------------------------------------------------
	call SolveMaxwells(vectorx,vectory,om,rho,fwdCtrls,errflag)


	!-----------------------------------------------
	! From <x>, compute the interior components of X
	!-----------------------------------------------
	vectorh = vectorx
	if (adjoint) then
	  call mult_vec_by_S(nx,ny,nz,vectorh,grid%x,grid%y,grid%z)
	else
	  call divide_vec_by_l(nx,ny,nz,vectorh,grid%x,grid%y,grid%z)
	end if

	!--------------------------------------------------------------------
	! Boundary conditions: in the future, a calculation will be performed
	!--------------------------------------------------------------------
	if (adjoint) then
	  ! Xb will be computed from X_i and Y_b using the expression
	  ! $X_b = - \length{b} R_\rho^T \area{i}^{-1} X_i + Y_b$
	  ! Since $R_\rho^T$ has not yet been implemented, and since we
	  ! do not use X_b for computing data sensitivities, assume zero.
	  Xb%c = C_ZERO
	  if (.not.delta) then
		write (0, *) 'Warning: (operatorM) Xb has been set to zero; it is not zero'
	  end if
	else
	  Xb = Yb
	end if

	!---------------------
	! Copy everything to X
	!---------------------
	call copyd1_d3_d(vectorh,Xfull,grid,bc=Xb)

	!--------------
	! Done; exiting
	!--------------
	call deall_sparsevecc(Xb)
	call deall_sparsevecc(Yb)
	deallocate(vectorx,vectory,vectorh,vectorb,vectors)
	return

  end subroutine operatorM	! operatorM


  ! ***************************************************************************
  ! * operator D_{S_i}^{-1}: E -> E_i represents the diagonal operator that
  ! * pre-divides the interior components of the input vector X by unit area,
  ! * and nullifies the boundary components to obtain a vector of interior
  ! * components of S^{-1} X. Here the output will include the boundary values,
  ! * which will be zero.

  subroutine operatorD_Si_divide(Xfull,grid)

	type (cvector), intent(inout)			  :: Xfull
	type (grid_t), intent(in)			  :: grid
	complex(8),dimension(:),allocatable		  :: vectorx
	integer									  :: istat,nx,ny,nz

	nx = grid%nx
	ny = grid%ny
	nz = grid%nz

	allocate(vectorx(np2),STAT=istat)
	call copyd3_d1_d(vectorx,Xfull,grid)  ! extract interior components
	call divide_vec_by_S(nx,ny,nz,vectorx,grid%x,grid%y,grid%z)
	call copyd1_d3_d(vectorx,Xfull,grid)  ! map back with zero b.c.
	deallocate(vectorx)

  end subroutine operatorD_Si_divide


  ! ***************************************************************************
  ! * operator D_{l}^{-1}: E -> E represents the diagonal operator that
  ! * pre-divides all components of the input vector X by unit lengths,

  subroutine operatorD_l_divide(Xfull,grid)

	use boundaries

	type (cvector), intent(inout)			  :: Xfull
	type (sparsevecc)						  :: Xb,bc
	type (grid_t), intent(in)			  :: grid
	complex(8),dimension(:),allocatable		  :: vectorx
	integer									  :: istat,nx,ny,nz

	nx = grid%nx
	ny = grid%ny
	nz = grid%nz

	call extractBC(Xb,Xfull)
	call divide_bc_by_l(Xb,bc)
	allocate(vectorx(np2),STAT=istat)
	call copyd3_d1_d(vectorx,Xfull,grid)  ! extract interior components
	call divide_vec_by_l(nx,ny,nz,vectorx,grid%x,grid%y,grid%z)
	call copyd1_d3_d(vectorx,Xfull,grid,bc=bc)  ! map back with zero b.c.
	deallocate(vectorx)
	call deall_sparsevecc(Xb)
	call deall_sparsevecc(bc)

  end subroutine operatorD_l_divide


  ! ***************************************************************************
  ! * operator D_{l}: E -> E represents the diagonal operator that
  ! * pre-multiplies all components of the input vector X by unit lengths,

  subroutine operatorD_l_mult(Xfull,grid)

	use boundaries

	type (cvector), intent(inout)			  :: Xfull
	type (sparsevecc)						  :: Xb,bc
	type (grid_t), intent(in)			  :: grid
	complex(8),dimension(:),allocatable		  :: vectorx
	integer									  :: istat,nx,ny,nz

	nx = grid%nx
	ny = grid%ny
	nz = grid%nz

	call extractBC(Xb,Xfull)
	call mult_bc_by_l(Xb,bc)
	allocate(vectorx(np2),STAT=istat)
	call copyd3_d1_d(vectorx,Xfull,grid)  ! extract interior components
	call mult_vec_by_l(nx,ny,nz,vectorx,grid%x,grid%y,grid%z)
	call copyd1_d3_d(vectorx,Xfull,grid,bc=bc)  ! map back with zero b.c.
	deallocate(vectorx)
	call deall_sparsevecc(Xb)
	call deall_sparsevecc(bc)

  end subroutine operatorD_l_mult


  ! ***************************************************************************
  ! * operator lC is the full curl operator, lC: E -> F
  ! * It acts on a complex vector defined on edges,
  ! * producing the respective vector defined on faces.
  ! * It is an implementation of the following computations:
  ! $e_\phi(i,j,k)=h_r(i,j,k) + h_\th(i,j,k+1) - h_r(i,j+1,k) - h_\th(i,j,k)$
  ! $e_\th(i,j,k)= - h_r(i,j,k) + h_\phi(i,j,k) + h_r(i+1,j,k) - h_\phi(i,j,k+1)$
  ! $e_r(i,j,k)=h_\th(i,j,k) + h_\phi(i,j+1,k) - h_\th(i+1,j,k) - h_\phi(i,j,k)$
  ! * where h is the vector field defined on edges, pre-multiplied by edge lengths.

  subroutine operatorlC(vecE,vecF,grid)

    implicit none
	type (cvector), intent(in)					 :: vecE
	type (cvector), intent(inout)					 :: vecF
	type (grid_t), intent(in)				 :: grid
    real(8)										 :: xlen,xlenjp,xlenkp
    real(8)										 :: ylen,ylenip,ylenkp
	real(8)										 :: zlen,zlenip,zlenjp
    integer										 :: i,j,k,istat
	integer										 :: nx,ny,nz
	real(8), dimension(:), allocatable			 :: x,y,z

    if (.not.vecE%allocated) then

 	  write(0,*) 'Error: (operatorC) vecE not allocated'
 	  stop

	else if (vecE%gridType /= EDGE) then

 	  write(0,*) 'Error: (operatorC) input vector is not defined on edges'
 	  stop

    endif

    if (.not.vecF%allocated) then

 	  call create_cvector(grid,vecF,FACE)

	else if (vecF%gridType /= FACE) then

 	  write(0,*) 'Error: (operatorC) output vector is not defined on faces'
 	  stop

    endif

	nx = grid%nx
	ny = grid%ny
	nz = grid%nz

	allocate(x(nx),y(ny+1),z(nz+1),STAT=istat)
	x = grid%x
	y = grid%y
	z = grid%z

	! Boundary values of h at k=1 and k=nz+1 are fixed during initialization;
	! loop over radii; do not loop over k=nz+1 (fields undefined at nz+2).
    do k=1,nz
	  call leng_zijk(k,z,zlen)
	  zlenip=zlen
	  zlenjp=zlen
	  ! loop over co-latitudes from the North pole to the South
      do j=1,ny
		call leng_yijk(j,k,y,z,ylen)
		call leng_yijk(j,k+1,y,z,ylenkp)
		ylenip=ylen
		! loop over longitudes including i=nx (nx+1'th fields same as i=1)
        do i=1,nx
		  call leng_xijk(i,j,k,x,y,z,xlen)
		  call leng_xijk(i,j+1,k,x,y,z,xlenjp)
		  call leng_xijk(i,j,k+1,x,y,z,xlenkp)

		  vecF%x(i,j,k) = zlen*vecE%z(i,j,k) + ylenkp*vecE%y(i,j,k+1) &
						- zlenjp*vecE%z(i,j+1,k) - ylen*vecE%y(i,j,k)

		  vecF%y(i,j,k) = - zlen*vecE%z(i,j,k) + xlen*vecE%x(i,j,k) &
						+ zlenip*vecE%z(i+1,j,k) - xlenkp*vecE%x(i,j,k+1)

		  vecF%z(i,j,k) = ylen*vecE%y(i,j,k) + xlenjp*vecE%x(i,j+1,k) &
						- ylenip*vecE%y(i+1,j,k) - xlen*vecE%x(i,j,k)

         end do
      end do
    end do

	! x-component at nx+1 (zero longitude)
	vecF%x(nx+1,:,:) = vecF%x(1,:,:)

	! y-component at ny+1 (South pole): undefined
	vecF%y(:,ny+1,:) = C_ZERO

	! z-component at nz+1 (lower domain boundary)
	k=nz+1
      do j=1,ny
		call leng_yijk(j,k,y,z,ylen)
		ylenip=ylen
        do i=1,nx
		  call leng_xijk(i,j,k,x,y,z,xlen)
		  call leng_xijk(i,j+1,k,x,y,z,xlenjp)
		  vecF%z(i,j,k) = ylen*vecE%y(i,j,k) + xlenjp*vecE%x(i,j+1,k) &
						- ylenip*vecE%y(i+1,j,k) - xlen*vecE%x(i,j,k)
         end do
      end do


    deallocate(x,y,z)

  end subroutine operatorlC	! lC


  ! ***************************************************************************
  ! * operator C, C: E -> F, represents the multiplication by a sparse matrix
  ! * with non-zero values equal to +/- 1.
  ! * It acts on a complex vector defined on edges,
  ! * producing the respective vector defined on faces.
  ! * It is an implementation of the following computations:
  ! $e_\phi(i,j,k)=h_\th(i,j,k) + h_r(i,j+1,k) - h_\th(i,j,k+1) - h_r(i,j,k)$
  ! $e_\th(i,j,k)= h_r(i,j,k) + h_\phi(i,j,k+1) - h_r(i+1,j,k) - h_\phi(i,j,k)$
  ! $e_r(i,j,k)=h_\phi(i,j,k) + h_\th(i+1,j,k) - h_\phi(i,j+1,k) - h_\th(i,j,k)$
  ! * where h is the input vector (normally, edge lengths time the magnetic field
  ! * defined on edges).

  subroutine operatorC(vecE,vecF,grid)

    implicit none
	type (cvector), intent(inout)				 :: vecE
	type (cvector), intent(inout)					 :: vecF
	type (grid_t), intent(in)				 :: grid
	logical										 :: verbose
    integer										 :: i,j,k,istat
	integer										 :: nx,ny,nz
	real(8), dimension(:), allocatable			 :: x,y,z

    if (.not.vecE%allocated) then

 	  write(0,*) 'Error: (operatorC) vecE not allocated'
 	  stop

	else if (vecE%gridType /= EDGE) then

 	  write(0,*) 'Error: (operatorC) input vector is not defined on edges'
 	  stop

    endif

    if (.not.vecF%allocated) then

 	  call create_cvector(grid,vecF,FACE)

	else if (vecF%gridType /= FACE) then

 	  write(0,*) 'Error: (operatorC) output vector is not defined on faces'
 	  stop

    endif

	nx = grid%nx
	ny = grid%ny
	nz = grid%nz

	allocate(x(nx),y(ny+1),z(nz+1),STAT=istat)
	x = grid%x
	y = grid%y
	z = grid%z

	! Set the undefined values to zero just in case they are non-zero
	! (which should never happen! - unless a test vector is used)
	!vecE%x(:,1,:) = C_ZERO
	!vecE%x(:,ny+1,:) = C_ZERO

	! Set the repetitious values in case they are not already correct
	!vecE%y(nx+1,:,:) = vecE%y(1,:,:)
	!vecE%z(nx+1,:,:) = vecE%z(1,:,:)
	call validate_cvector(vecE,verbose)


  ! $e_\phi(i,j,k)=h_\th(i,j,k) + h_r(i,j+1,k) - h_\th(i,j,k+1) - h_r(i,j,k)$
  ! $e_\th(i,j,k)= h_r(i,j,k) + h_\phi(i,j,k+1) - h_r(i+1,j,k) - h_\phi(i,j,k)$
  ! $e_r(i,j,k)=h_\phi(i,j,k) + h_\th(i+1,j,k) - h_\phi(i,j+1,k) - h_\th(i,j,k)$

	! Boundary values of h at k=1 and k=nz+1 are fixed during initialization;
	! loop over radii; do not loop over k=nz+1 (fields undefined at nz+2).
    do k=1,nz
	  ! loop over co-latitudes from the North pole to the South
      do j=1,ny
		! loop over longitudes including i=nx (nx+1'th fields same as i=1)
        do i=1,nx

		  vecF%x(i,j,k) = vecE%y(i,j,k) + vecE%z(i,j+1,k) &
						  - vecE%y(i,j,k+1) - vecE%z(i,j,k)

		  vecF%y(i,j,k) = vecE%z(i,j,k) + vecE%x(i,j,k+1) &
						  - vecE%z(i+1,j,k) - vecE%x(i,j,k)

		  vecF%z(i,j,k) = vecE%x(i,j,k) + vecE%y(i+1,j,k) &
						  - vecE%x(i,j+1,k) - vecE%y(i,j,k)

         end do
      end do
    end do

	! x-component at nx+1 (zero longitude)
	vecF%x(nx+1,:,:) = vecF%x(1,:,:)

	! y-component at ny+1 (South pole): undefined
	vecF%y(:,ny+1,:) = C_ZERO

	! z-component at nz+1 (lower domain boundary)
	k=nz+1
      do j=1,ny
        do i=1,nx
		  vecF%z(i,j,k) = vecE%x(i,j,k) + vecE%y(i+1,j,k) &
						  - vecE%x(i,j+1,k) - vecE%y(i,j,k)
         end do
      end do

	! z-component at North pole: undefined
	!vecF%z(:,1,:) = C_ZERO

	call validate_cvector(vecF)

    deallocate(x,y,z)

  end subroutine operatorC	! C


  ! ***************************************************************************
  ! * operator Ct, Ct: F -> E, maps onto center edge of the "paddle wheel".
  ! * It acts on a complex vector defined on faces,
  ! * producing the respective vector defined on edges.
  ! * It is an implementation of the following computations:
  ! $h_\phi(i,j,k)=e_r(i,j,k) - e_\th(i,j,k) - e_r(i,j-1,k) + e_\th(i,j,k-1)$
  ! $h_\th(i,j,k)= e_\phi(i,j,k) - e_r(i,j,k) - e_\phi(i,j,k-1) + e_r(i-1,j,k)$
  ! $h_r(i,j,k)=e_\th(i,j,k) - e_\phi(i,j,k) - e_\th(i-1,j,k) + e_\phi(i,j-1,k)$
  ! * where e is the input vector defined on faces.
  ! * This operator is not required in the Jacobian computations. Currently
  ! * used for testing purposes only.

  subroutine operatorCt(vecF,vecE,grid)

    implicit none
	type (cvector), intent(inout)				 :: vecF
	type (cvector), intent(inout)					 :: vecE
	type (grid_t), intent(in)				 :: grid
	logical										 :: verbose
    integer										 :: i,j,k,istat
	integer										 :: nx,ny,nz
	complex(8)									 :: total
	real(8), dimension(:), allocatable			 :: x,y,z

    if (.not.vecF%allocated) then

 	  write(0,*) 'Error: (operatorCt) vecF not allocated'
 	  stop

	else if (vecF%gridType /= FACE) then

 	  write(0,*) 'Error: (operatorCt) input vector is not defined on faces'
 	  stop

    endif

    if (.not.vecE%allocated) then

 	  call create_cvector(grid,vecE,EDGE)

	else if (vecE%gridType /= EDGE) then

 	  write(0,*) 'Error: (operatorCt) output vector is not defined on edges'
 	  stop

    endif

	nx = grid%nx
	ny = grid%ny
	nz = grid%nz

	allocate(x(nx),y(ny+1),z(nz+1),STAT=istat)
	x = grid%x
	y = grid%y
	z = grid%z

	! Set the undefined values to zero just in case they are non-zero
	! (which should never happen! - unless a test vector is used)
	!vecF%y(:,1,:) = C_ZERO
	!vecF%y(:,ny+1,:) = C_ZERO

	! Set the repetitious values in case they are not already correct
	!vecF%x(nx+1,:,:) = vecF%x(1,:,:)
	call validate_cvector(vecF)

	! x-component

  ! $h_\phi(i,j,k)=e_r(i,j,k) - e_\th(i,j,k) - e_r(i,j-1,k) + e_\th(i,j,k-1)$
  ! $h_\th(i,j,k)= e_\phi(i,j,k) - e_r(i,j,k) - e_\phi(i,j,k-1) + e_r(i-1,j,k)$
  ! $h_r(i,j,k)=e_\th(i,j,k) - e_\phi(i,j,k) - e_\th(i-1,j,k) + e_\phi(i,j-1,k)$

	! loop over radii; do not loop over k=1 (fields undefined at k-1).
    do k=2,nz
	  ! loop over co-latitudes from the North pole to the South
      do j=2,ny
		! zero longitude (nx+1'th fields same as i=1)
        do i=1,nx
		  vecE%x(i,j,k) = vecF%z(i,j,k) + vecF%y(i,j,k-1) &
						- vecF%z(i,j-1,k) - vecF%y(i,j,k)
		end do
	  end do
	end do
	k = 1
	  do j=2,ny
		do i=1,nx
		  vecE%x(i,j,k) = vecF%z(i,j,k) &
						- vecF%z(i,j-1,k) - vecF%y(i,j,k)
		end do
	  end do
	k = nz+1
	  do j=2,ny
		do i=1,nx
		  vecE%x(i,j,k) = vecF%z(i,j,k) + vecF%y(i,j,k-1) &
						- vecF%z(i,j-1,k)
		end do
	  end do
	vecE%x(:,1,:) = C_ZERO	!undefined
	vecE%x(:,ny+1,:) = C_ZERO	!undefined

	! y-component

    do k=2,nz
      do j=1,ny
		vecE%y(1,j,k) = - vecF%z(1,j,k) + vecF%x(1,j,k) &
					  - vecF%x(1,j,k-1)
        do i=2,nx
		  vecE%y(i,j,k) = - vecF%z(i,j,k) + vecF%x(i,j,k) &
						+ vecF%z(i-1,j,k) - vecF%x(i,j,k-1)
		end do
		vecE%y(nx+1,j,k) = vecF%z(nx,j,k)
	  end do
	end do
	k = 1
	  do j=1,ny
		vecE%y(1,j,k) = - vecF%z(1,j,k) + vecF%x(1,j,k)
		do i=2,nx
		  vecE%y(i,j,k) = - vecF%z(i,j,k) + vecF%x(i,j,k) &
						+ vecF%z(i-1,j,k)
		end do
		vecE%y(nx+1,j,k) = vecF%z(nx,j,k)
	  end do
	k = nz+1
	  do j=1,ny
		vecE%y(1,j,k) = - vecF%z(1,j,k) - vecF%x(1,j,k-1)
		do i=2,nx
		  vecE%y(i,j,k) = - vecF%z(i,j,k) &
						+ vecF%z(i-1,j,k) - vecF%x(i,j,k-1)
		end do
		vecE%y(nx+1,j,k) = vecF%z(nx,j,k)
	  end do

	! z-component

    do j=2,ny
      do k=1,nz
		vecE%z(1,j,k) = vecF%y(1,j,k) + vecF%x(1,j-1,k) &
					   - vecF%x(1,j,k)
        do i=2,nx
		  vecE%z(i,j,k) = vecF%y(i,j,k) + vecF%x(i,j-1,k) &
						- vecF%y(i-1,j,k) - vecF%x(i,j,k)
		end do
		vecE%z(nx+1,j,k) = - vecF%y(nx,j,k)
	  end do
	end do
	j = 1
	  do k=1,nz
		do i=1,nx
		  vecE%z(i,j,k) = - vecF%x(i,j,k)
		end do
		vecE%z(nx+1,j,k) = C_ZERO
	  end do
	j = ny+1
	  do k=1,nz
		do i=1,nx
		  vecE%z(i,j,k) = vecF%x(i,j-1,k)
		end do
		vecE%z(nx+1,j,k) = C_ZERO
	  end do

	! Map back from full set of edges to unique edges

	do k=1,nz
	  vecE%z(1,1,k) = sum(vecE%z(:,1,k))
	  vecE%z(1,2:ny,k) = vecE%z(1,2:ny,k) + vecE%z(nx+1,2:ny,k)
	  vecE%z(1,ny+1,k) = sum(vecE%z(:,ny+1,k))
	end do

	do k=1,nz+1
	  vecE%y(1,:,k) = vecE%y(1,:,k) + vecE%y(nx+1,:,k)
	end do

	call validate_cvector(vecE)


    deallocate(x,y,z)

  end subroutine operatorCt	! This operator is the transpose of C


  ! ***************************************************************************
  ! * operator L is the mapping from cells onto faces, L: G -> F
  ! * This operator is the implementation of the following mapping:
  ! * $\rho -> (1/2*S)({l^+ \rho^+} + {l^- \rho^-})$

  subroutine operatorL(resist,vecF,grid)

    implicit none
	real(8),dimension(:,:,:),intent(in)			 :: resist	 !(nx,ny,nz)
	type (rvector), intent(inout)					 :: vecF
	type (grid_t), intent(in)				 :: grid
    real(8)										 :: lm,lp,S
	integer										 :: i,j,k,istat
	integer										 :: nx,ny,nz
	real(8), dimension(:), allocatable			 :: x,y,z

    if (.not.vecF%allocated) then

 	  call create_rvector(grid,vecF,FACE)

	else if (vecF%gridType /= FACE) then

 	  write(0,*) 'Error: (operatorL) output vector is not defined on faces'
 	  stop

	else

	  vecF%x = R_ZERO
	  vecF%y = R_ZERO
	  vecF%z = R_ZERO

    endif

	nx = grid%nx
	ny = grid%ny
	nz = grid%nz

	allocate(x(nx),y(ny+1),z(nz+1),STAT=istat)
	x = grid%x
	y = grid%y
	z = grid%z


    do k=1,nz
	  ! loop over co-latitudes from the North pole to the South
      do j=1,ny
		! loop over longitudes including i=nx (nx+1'th fields same as i=1)
        do i=1,nx
		  call leng_xijk2(i,j,k,x,y,z,lp)
		  if (i==1) then
			call leng_xijk2(nx,j,k,x,y,z,lm)
		  else
			call leng_xijk2(i-1,j,k,x,y,z,lm)
		  end if
          call area_sijk(j,k,y,z,S)

		  if (i==1) then
			vecF%x(i,j,k)=(lp*resist(i,j,k)+lm*resist(nx,j,k))/(2*S)
		  else
			vecF%x(i,j,k)=(lp*resist(i,j,k)+lm*resist(i-1,j,k))/(2*S)
		  end if

		  call leng_yijk2(j,k,y,z,lp)
		  if (j==1) then
		  else
			call leng_yijk2(j-1,k,y,z,lm)
		  end if
          call area_sjki(i,j,k,x,y,z,S)

		  if (j==1) then
			vecF%y(i,j,k)=R_ZERO  !undefined
		  else
			vecF%y(i,j,k)=(lp*resist(i,j,k)+lm*resist(i,j-1,k))/(2*S)
		  end if

		  call leng_zijk2(k,z,lp)
		  if (k==1) then
			lm=lp
		  else
			call leng_zijk2(k-1,z,lm)
		  end if
          call area_skij(i,j,k,x,y,z,S)

		  if (k==1) then
			vecF%z(i,j,k)=(lp*resist(i,j,k))/(2*S)
		  else
			vecF%z(i,j,k)=(lp*resist(i,j,k)+lm*resist(i,j,k-1))/(2*S)
		  end if

		end do
	  end do
	end do

	vecF%x(nx+1,:,:) = vecF%x(1,:,:)
	vecF%y(:,ny+1,:) = R_ZERO !undefined

	call leng_zijk2(nz,z,lm)
    do j=1,ny
      do i=1,nx
        call area_skij(i,j,nz+1,x,y,z,S)
		vecF%z(i,j,nz+1) = lm*resist(i,j,nz)/(2*S)
	  end do
	end do

	deallocate(x,y,z)

  end subroutine operatorL	! L


  ! ***************************************************************************
  ! * operator Lt is the mapping from faces onto cells, L^t: F -> G

  subroutine operatorLt(resist,vecF,grid)

    implicit none
	real(8),dimension(:,:,:),intent(inout)		 :: resist	 !(nx,ny,nz)
	type (rvector), intent(inout)				 :: vecF
	type (grid_t), intent(in)				 :: grid
    real(8)										 :: l,S,Sp
	integer										 :: i,j,k,istat
	integer										 :: nx,ny,nz
	real(8), dimension(:), allocatable			 :: x,y,z

    if (.not.vecF%allocated) then

 	  write(0,*) 'Error: (operatorLt) input vector is not allocated'
 	  stop

	else if (vecF%gridType /= FACE) then

 	  write(0,*) 'Error: (operatorLt) input vector is not defined on faces'
 	  stop

    endif

	nx = grid%nx
	ny = grid%ny
	nz = grid%nz

	allocate(x(nx),y(ny+1),z(nz+1),STAT=istat)
	x = grid%x
	y = grid%y
	z = grid%z

	resist = R_ZERO

	! making sure the input vector makes scientific sense...
	vecF%x(nx+1,:,:) = vecF%x(1,:,:)
	vecF%y(:,ny+1,:) = R_ZERO !undefined

    do k=1,nz
	  ! loop over co-latitudes from the North pole to the South
      do j=1,ny
		! loop over longitudes including i=nx (nx+1'th fields same as i=1)
        do i=1,nx
		  call leng_xijk2(i,j,k,x,y,z,l)
          call area_sijk(j,k,y,z,S)
		  Sp=S
		  resist(i,j,k)=vecF%x(i,j,k)*l/(2*S)+vecF%x(i+1,j,k)*l/(2*Sp)

		  call leng_yijk2(j,k,y,z,l)
          call area_sjki(i,j,k,x,y,z,S)
          call area_sjki(i,j+1,k,x,y,z,Sp)
		  if (j==1) then
			resist(i,j,k)=resist(i,j,k)+vecF%y(i,j+1,k)*l/(2*Sp)
		  else if (j==ny) then
			resist(i,j,k)=resist(i,j,k)+vecF%y(i,j,k)*l/(2*S)
		  else
  			resist(i,j,k)=resist(i,j,k)+vecF%y(i,j,k)*l/(2*S)+vecF%y(i,j+1,k)*l/(2*Sp)
		  end if

		  call leng_zijk2(k,z,l)
          call area_skij(i,j,k,x,y,z,S)
          call area_skij(i,j,k+1,x,y,z,Sp)
		  resist(i,j,k)=resist(i,j,k)+vecF%z(i,j,k)*l/(2*S)+vecF%z(i,j,k+1)*l/(2*Sp)

		end do
	  end do
	end do

	! North pole
	j = 1
	do k=2,nz
	  do i=1,nx
	  end do
	end do

	! South pole
	j = ny
	do k=2,nz
	  do i=1,nx
	  end do
	end do

	! Uppermost grid layer
	k = 1
	do j=1,ny
	  do i=1,nx
		if (j==1) then
		else if (j==ny) then
		else
		end if
	  end do
	end do

	! Lowermost grid layer
	k = nz
	do j=1,ny
	  do i=1,nx
		if (j==1) then
		else if (j==ny) then
		else
		end if
	  end do
	end do


	deallocate(x,y,z)

  end subroutine operatorLt	! This operator is the transpose of L


  ! ***************************************************************************
  ! * operator P is the mapping from the model parameters to cell centres,
  ! * P: R^n -> G; by definition, \drho = P \da.
  ! * vector p_j is the j'th column of P, that corresponds to \pd{\rho}{a_j}.
  ! * p_j \In G.

  function vectorPj(da,n) result (dm)

	use global	! uses grid,param,rho

    type (modelParam_t), intent(inout)				:: da
	integer, intent(in)								:: n
	type (rscalar)									:: dm !(nx,ny,nz)
	integer											:: i,j,k,l,istat
	real(8)											:: value
	type (modelLayer_t), pointer						:: this_layer
	type (modelPoint_t)								:: point
	type (modelFunc_t)								:: func
	type (modelCoeff_t)								:: coeff

	! Test to make sure no grid is defined outside the layered region
	if (grid%r(grid%nz+1) < param%L(param%nL)%lbound) then
	  param%L(param%nL)%lbound = grid%r(grid%nz+1)
	end if

	! Create a zero-valued output in G
	call create_rscalar(grid,dm,CENTER)

	coeff = getCoeff_modelParam(da,n)

	if (coeff%frozen) then
	  return
	end if

	this_layer => coeff%L

	! Resistivity is constant at air layers, hence derivative is zero
	forall (i=1:grid%nx, j=1:grid%ny, k=1:grid%nzCrust)
	  dm%v(i,j,k) = 0.0d0
	end forall

	do k=grid%nzCrust+1,grid%nz

	  if (.not.in_layer(grid%r(k),coeff%L)) then
		cycle
	  end if

	  do i=1,grid%nx
		do j=1,grid%ny

		  point%phi   = (grid%ph(i) + grid%ph(i+1))/2
		  point%theta = (grid%th(j) + grid%th(j+1))/2
		  point%r     = (grid%r(k) + grid%r(k+1))/2

		  func = coeff%F
		  value = F_at_point(func,point)

		  if (this_layer%if_log) then
			dm%v(i,j,k) = value*rho(i,j,k)*log(10.)
		  else
			dm%v(i,j,k) = value
		  end if

		end do
	  end do
	end do

  end function vectorPj	! j'th column of P


  ! ***************************************************************************
  ! * operator P is the mapping from the model parameters to cell centres,
  ! * P: R^n -> G; by definition, \drho = P \da.
  ! * We define $\tau^l_{ijk}=\drho_{ijk}/\da_l$. Hence $\tau_l$ is the
  ! * l'th column of operator P, and therefore the l'th row of Pt.
  ! * Therefore, we obtain $\da_l=\sum_{i,j,k} \tau^l_{ijk}\drho_{ijk}$.
  ! * We compute $\tau_l$ for the following two cases:
  ! * 1)		 $\rho_{ijk}  = \sum_l a_l F_l(\phi,\th,r)$,
  ! * 2) $\log_10(\rho_{ijk}) = \sum_l a_l F_l(\phi,\th,r)$,
  ! * where (phi,th,r) is the (i,j,k)'th cell centre.
  ! * In the first case, $\tau^l_{ijk}=F_l(\phi,\th,r)$;
  ! * in the second case, $\tau^l_{ijk}=\rho_{ijk} F_l(\phi,\th,r) \log(10)$.
  ! * The rho, drho, dprm are global variables defined in module modeldef
  ! * Functions to substitute for F are declared in module paramfunc
  ! *
  ! * initModel is the routine that generates the 3-D resistivity map on the
  ! * grid using the information stored in the grid and the parametrization

  subroutine operatorP(da,dm)

	use global	! uses grid,param,rho

    type (modelParam_t), intent(in)					:: da
	type (rscalar), intent(inout)						:: dm !(nx,ny,nz)
	!real(8), dimension(:,:,:), intent(inout)		:: dm !(nx,ny,nz)
	integer											:: i,j,k,l,istat
	integer											:: iL,ip
	real(8)											:: value,coeff
	type (modelLayer_t), pointer						:: this_layer
	type (modelPoint_t)								:: point
	type (modelFunc_t)								:: func

	! Create the output resistivity model on the grid
	call create_rscalar(grid,dm,CENTER)

	! Test to make sure no grid is defined outside the layered region
	if (grid%r(grid%nz+1) < param%L(param%nL)%lbound) then
	  param%L(param%nL)%lbound = grid%r(grid%nz+1)
	end if

	! Resistivity is constant at air layers, hence derivative is zero
	forall (i=1:grid%nx, j=1:grid%ny, k=1:grid%nzCrust)
	  dm%v(i,j,k) = 0.0d0
	end forall

	do k=grid%nzCrust+1,grid%nz

	  ! Find current layer by locating the upper boundary of a cell
	  do l=1,param%nL
		if (in_layer(grid%r(k),param%L(l))) then
		  this_layer => param%L(l)
		  exit
		end if
	  end do

	  iL = this_layer%num

	  do i=1,grid%nx
		do j=1,grid%ny

		  point%phi   = (grid%ph(i) + grid%ph(i+1))/2
		  point%theta = (grid%th(j) + grid%th(j+1))/2
		  point%r     = (grid%r(k) + grid%r(k+1))/2

		  dm%v(i,j,k) = 0.0d0

		  ! Sum up the coeffs * F_at_point in the given layer
		  value = 0.0d0
		  do ip=1,param%nF

			if(da%c(iL,ip)%frozen) then
			  cycle
			end if
			coeff = da%c(iL,ip)%value
			func = da%F(ip)
			value = F_at_point(func,point)

			if (this_layer%if_log) then
			  dm%v(i,j,k) = dm%v(i,j,k) + value*coeff*rho(i,j,k)*log(10.)
			else
			  dm%v(i,j,k) = dm%v(i,j,k) + value*coeff
			end if

		  end do

		end do
	  end do
	end do

  end subroutine operatorP	! P


  ! ***************************************************************************
  ! * operator Pt is the mapping from cells to the model parameters,
  ! * P^t: G -> R^n
  ! * This is an implementation of \da^t * P^t = \drho^t
  ! * In the general case, P^t is a real matrix $n\times \mod{\mathbb{G}}}$
  ! * defined as the transpose of $P = \drho/\da$.
  ! * Its' element $\tau_l(ijk) = \drho(ijk)/\da_l$.
  ! *
  ! * Output vector size equals to the number of parametrization coefficients.
  ! * The values are only computed for variable parameters, otherwize they are
  ! * set to zero. This is done so that it would be easy to map back to the
  ! * original parametrization structure when we output and otherwise use these
  ! * values. It is easy to extract the components corresponding to the variable
  ! * parameters only:
  ! * count = 1
  ! * do index = 1,size(vecN)
  ! *   if (.not.param%p(index)%frozen) then
  ! *	  value(count) = vecN(index)
  ! *	  count = count + 1
  ! *	end if
  ! * end do

  subroutine operatorPt(dm,da)

	! uses: rho, param, grid
	use global
    implicit none
	type (rscalar), intent(in)						:: dm !(nx,ny,nz)
    type (modelParam_t), intent(inout)					:: da
	!real(8),dimension(:,:,:),intent(in)				 :: dm  !(nx,ny,nz)
	!real(8),dimension(:),intent(out)				 :: da  !ncoeff
	integer											 :: i,j,k,l
	integer											 :: iL,ip
	real(8)											 :: tau ! single entry of matrix P^t
	type (modelPoint_t)								 :: point
	type (modelFunc_t)								 :: func
	type (modelLayer_t)								 :: this_layer

!	if(size(dm) /= grid%nx * grid%ny * grid%nz) then
!		write(0, *) 'Error: (operatorPt) input vector should be defined at every grid cell'
!		stop
!	end if

!	if(size(da) /= param%np) then
!		write(0, *) 'Error: (operatorPt) output vector size should = # of parametrization coefficients'
!		stop
!	end if

!	if(size(da) /= count(.not.param%p(1:param%np)%frozen)) then
!		write(0, *) 'Error: (operatorPt) output vector size should = # of variable parameters'
!		stop
!	end if

	da = param
	call zero(da)

	do iL = 1,param%nL

	  this_layer = param%L(iL)

	  do ip = 1,param%nF

		! If this parameter is frozen, the derivative is zero, cycle
		if (param%c(iL,ip)%frozen) then
		  cycle
		end if

		! Otherwise find the functional
		func = param%c(iL,ip)%F

		! Going vertically down through the chosen layer
		do k=grid%nzCrust+1,grid%nz

		  ! If grid radius is not in this layer, ignore
		  if(.not.in_layer(grid%r(k),this_layer)) then
			cycle
		  end if
		  ! Otherwise, compute an expression for each cell
		  do i=1,grid%nx
			do j=1,grid%ny
			  point%phi   = (grid%ph(i) + grid%ph(i+1))/2
			  point%theta = (grid%th(j) + grid%th(j+1))/2
			  point%r     = (grid%r(k) + grid%r(k+1))/2

			  tau = F_at_point(func,point)
			  if (this_layer%if_log) then
				tau = tau * rho(i,j,k) * log(10.0d0)
			  end if
			  ! Add this expression to the output vector component
			  da%c(iL,ip)%value = da%c(iL,ip)%value + tau * dm%v(i,j,k)
			end do
		  end do

		end do
	  end do
	end do

  end subroutine operatorPt	! This operator is the transpose of P



end module jacobian
